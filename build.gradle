buildscript {
    repositories {
        jcenter()
        dependencies {
            classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.50'
            classpath 'ru.yoomoney.gradle.plugins:artifact-release-plugin:3.4.0'
        }
    }
}

plugins {
    id "com.jfrog.bintray" version "1.8.4"
    id "com.gradle.plugin-publish" version "0.12.0"
    id "java-gradle-plugin"
    id "ru.yoomoney.gradle.plugins.artifact-release-plugin" version "3.4.0"
}

releaseSettings {
        releaseTasks = ["build", "bintrayUpload", "publishPlugins"]
        pathToGitPrivateSshKey = System.getenv("GIT_PRIVATE_SSH_KEY_PATH")
        passphraseToGitPrivateSshKey = System.getenv("GIT_KEY_PASSPHRASE")
        gitUsername = "yoomoney-robot"
        gitEmail = "SvcReleaserBackend@yoomoney.ru"
        changelogRequired = true
        addPullRequestLinkToChangelog = true
        pullRequestInfoProvider = "GitHub"
        githubAccessToken = System.getenv("GITHUB_TOKEN")
}

repositories {
    mavenCentral()
    jcenter()
}

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'kotlin'
apply from: 'dependencies.gradle'

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileJava {
    options.compilerArgs.addAll(['--release', '8'])
}

jar {
    manifest {
        attributes(
                'Implementation-Version': "$project.version",
                'Implementation-Title': "$project.name",
                'Built-By': java.lang.System.getProperty('user.name'),
                'Built-Date': new Date().format('yyyy-MM-dd'),
                'Copyright': '2021 NBCO YooMoney LLC',
                'License': 'MIT'
        )
    }

    from(projectDir.absolutePath) {
        include "CHANGELOG.md", "README.md"
        into "META-INF"
    }
}

apply plugin: 'idea'
idea {
    project {
        jdkName = '1.8'
        languageLevel = '1.8'
        vcs = 'Git'
    }
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}
test {
    systemProperty "file.encoding", "UTF-8"
    testLogging {
        events "skipped", "failed"
        exceptionFormat "full"
        showExceptions true
        showCauses true
        showStackTraces true
    }
}

apply plugin: "jacoco"
jacocoTestReport {
    reports {
        html.enabled = true
        xml.enabled = true
    }
}
check.dependsOn jacocoTestReport

javadoc {
    options {
        docEncoding = 'UTF-8'
        charSet = 'UTF-8'
        encoding = 'UTF-8'
        failOnError = true
    }
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

group = 'ru.yoomoney.gradle.plugins'


gradlePlugin {
    plugins {
        artifactReleasePlugin {
            id = 'ru.yoomoney.gradle.plugins.artifact-release-plugin'
            implementationClass = 'ru.yoomoney.gradle.plugins.release.ReleasePlugin'
            displayName = 'Artifact Release Plugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/yoomoney-gradle-plugins/artifact-release-plugin'
    vcsUrl = 'https://github.com/yoomoney-gradle-plugins/artifact-release-plugin.git'
    description = 'Gradle artifact release plugin'
    tags = ['release', 'gradle', 'plugin']
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId group
            artifactId 'artifact-release-plugin'
            version project.version
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']

    dryRun = false
    publish = true
    pkg {
        name = 'artifact-release-plugin'
        repo = 'maven'
        userOrg = 'yoomoney-gradle-plugins'
        desc = 'Gradle artifact release plugin'
        websiteUrl = 'https://github.com/yoomoney-gradle-plugins/artifact-release-plugin'
        issueTrackerUrl = 'https://github.com/yoomoney-gradle-plugins/artifact-release-plugin/issues'
        vcsUrl = 'https://github.com/yoomoney-gradle-plugins/artifact-release-plugin.git'
        licenses = ['MIT']
        labels = ['release', 'gradle', 'plugin']
        publicDownloadNumbers = true
        githubRepo = 'yoomoney-gradle-plugins/artifact-release-plugin'
        githubReleaseNotesFile = 'CHANGELOG.md'
        version {
            gpg {
                sign = true
                passphrase = System.getenv('BINTRAY_PASSPHRASE')
            }
            name = project.version
            released = new Date()
            vcsTag = project.version
        }
    }
}